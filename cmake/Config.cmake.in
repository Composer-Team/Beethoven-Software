@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/beethovenTargets.cmake")

check_required_components(beethoven)

# BeethovenHardware.cmake
# Provides beethoven_hardware() function for unified hardware generation
#
# Usage:
#   beethoven_hardware(my_accel
#     MAIN_CLASS com.example.MyAccelBuild
#     PLATFORM discrete                    # Optional: discrete|kria|baremetal (default: discrete)
#     BUILD_MODE Simulation                # Optional: Simulation|Synthesis (default: Simulation)
#     SCALA_ARGS -DPARAM=value            # Optional: additional args passed to sbt
#   )
#
# Environment:
#   BEETHOVEN_HARDWARE_PATH - Path to Beethoven-Hardware sbt project (required)

# Find the Beethoven-Hardware directory
# Search order:
# 1. BEETHOVEN_HARDWARE_PATH environment variable
# 2. Installed config file (share/beethoven/beethoven_paths.cmake)
# 3. Source tree relative paths

get_filename_component(_BEETHOVEN_CMAKE_DIR "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)
get_filename_component(_BEETHOVEN_PREFIX "${_BEETHOVEN_CMAKE_DIR}/../../.." ABSOLUTE)
set(BEETHOVEN_HARDWARE_DIR "")
set(BEETH_ENV "")
option(BUILD_LOCALLY OFF)

# 1. Check environment variable first
if(DEFINED ENV{BEETHOVEN_HARDWARE_PATH})
  set(BEETHOVEN_HARDWARE_DIR "$ENV{BEETHOVEN_HARDWARE_PATH}")
  message(STATUS "Using BEETHOVEN_HARDWARE_PATH from environment: ${BEETHOVEN_HARDWARE_DIR}")
endif()


message(STATUS "cmake check: ${CMAKE_SOURCE_DIR}/build.sbt")
if(NOT BEETHOVEN_HARDWARE_DIR)
  if(EXISTS "${CMAKE_SOURCE_DIR}/build.sbt")
    get_filename_component(BEETHOVEN_HARDWARE_DIR
      "${CMAKE_SOURCE_DIR}" ABSOLUTE)
    message(STATUS "Using Beethoven-Hardware from source tree: ${BEETHOVEN_HARDWARE_DIR}")
    set(BUILD_LOCALLY ON)
  endif()
endif()

if (${BUILD_LOCALLY})
  set(BEETH_ENV "export BEETHOVEN_PATH=${CMAKE_SOURCE_DIR}")
endif()


function(beethoven_hardware TARGET)
  cmake_parse_arguments(BH
    ""                              # Boolean options
    "MAIN_CLASS;PLATFORM;BUILD_MODE" # Single-value args
    "SCALA_ARGS"                     # Multi-value args
    ${ARGN}
  )

  # Check that Beethoven-Hardware was found
  if(NOT BEETHOVEN_HARDWARE_DIR OR NOT EXISTS "${BEETHOVEN_HARDWARE_DIR}/build.sbt")
    message(FATAL_ERROR
      "beethoven_hardware: Cannot find Beethoven-Hardware sbt project.\n"
      "Please set the BEETHOVEN_HARDWARE_PATH environment variable:\n"
      "  export BEETHOVEN_HARDWARE_PATH=/path/to/Beethoven-Hardware\n"
      "\n"
      "Or configure it during beethoven installation:\n"
      "  cmake .. -DPLATFORM=discrete -DBEETHOVEN_HARDWARE_PATH=/path/to/Beethoven-Hardware\n"
      "\n"
      "Searched locations:\n"
      "  - BEETHOVEN_HARDWARE_PATH env var\n"
      "  - Installed config file\n"
      "  - Source tree relative paths\n"
      "Current BEETHOVEN_HARDWARE_DIR: ${BEETHOVEN_HARDWARE_DIR}"
    )
  endif()

  # Validate required arguments
  if(NOT BH_MAIN_CLASS)
    message(FATAL_ERROR "beethoven_hardware: MAIN_CLASS is required")
  endif()

  # Set defaults
  if(NOT BH_PLATFORM)
    set(BH_PLATFORM "discrete")
  endif()
  if(NOT BH_BUILD_MODE)
    set(BH_BUILD_MODE "Simulation")
  endif()

  # Create local BEETHOVEN_PATH for this target
  set(LOCAL_BEETHOVEN_PATH "${CMAKE_SOURCE_DIR}")
  set(LOCAL_BUILD_DIR "${LOCAL_BEETHOVEN_PATH}/build")
  set(LOCAL_HW_DIR "${LOCAL_BUILD_DIR}/gen")

  # Generated output files
  set(GENERATED_HEADER "${LOCAL_BUILD_DIR}/beethoven_hardware.h")
  set(GENERATED_SOURCE "${LOCAL_BUILD_DIR}/beethoven_hardware.cc")
  set(GENERATED_CMAKE_SRCS "${LOCAL_BUILD_DIR}/cmake_srcs.cmake")
  set(GENERATED_VCS_SRCS "${LOCAL_BUILD_DIR}/vcs_srcs.in")

  # Create output directories
  file(MAKE_DIRECTORY "${LOCAL_BUILD_DIR}")
  file(MAKE_DIRECTORY "${LOCAL_HW_DIR}")

  # Build the sbt command
  set(SBT_RUN_CMD "runMain ${BH_MAIN_CLASS}")
  if(BH_SCALA_ARGS)
    string(JOIN " " SCALA_ARGS_STR ${BH_SCALA_ARGS})
    set(SBT_RUN_CMD "${SBT_RUN_CMD} ${SCALA_ARGS_STR}")
  endif()

  # Validate Beethoven-Hardware exists
  if(NOT EXISTS "${BEETHOVEN_HARDWARE_DIR}/build.sbt")
    message(FATAL_ERROR
      "beethoven_hardware: Cannot find Beethoven-Hardware at ${BEETHOVEN_HARDWARE_DIR}\n"
      "Set BEETHOVEN_HARDWARE_PATH environment variable to the correct location.")
  endif()

  # Create stamp file to track successful generation
  set(STAMP_FILE "${LOCAL_BUILD_DIR}/.beethoven_generated_${TARGET}")


  # Export variables for use by beethoven_testbench()
  set(${TARGET}_BEETHOVEN_PATH "${LOCAL_HW_DIR}/${TARGET}_HW_DIR/" PARENT_SCOPE)
  # Custom command to generate hardware
  # Sets BEETHOVEN_PATH locally for this invocation only
  add_custom_command(
    OUTPUT "${STAMP_FILE}"
    COMMAND ${CMAKE_COMMAND} -E echo "Generating Beethoven hardware: ${TARGET}"
    COMMAND ${CMAKE_COMMAND} -E echo "  Main class: ${BH_MAIN_CLASS}"
    COMMAND ${CMAKE_COMMAND} -E echo "  Platform: ${BH_PLATFORM}"
    COMMAND ${CMAKE_COMMAND} -E echo "  Build mode: ${BH_BUILD_MODE}"
    COMMAND ${CMAKE_COMMAND} -E echo "  Output: ${LOCAL_BEETHOVEN_PATH}"
    COMMAND ${CMAKE_COMMAND} -E env
    "BEETHOVEN_PATH=${LOCAL_HW_DIR}/${TARGET}_HW_DIR/"
    sbt "${SBT_RUN_CMD}"
    COMMAND ${CMAKE_COMMAND} -E touch "${STAMP_FILE}"
    WORKING_DIRECTORY "${BEETHOVEN_HARDWARE_DIR}"
    COMMENT "Generating Beethoven hardware for ${TARGET} (${BH_MAIN_CLASS})"
    VERBATIM
  )

  # Create a custom target for the hardware generation
  add_custom_target(${TARGET}_hw
    DEPENDS "${STAMP_FILE}"
  )


  message(STATUS "Configured Beethoven hardware target: ${TARGET}")
  message(STATUS "  Main class: ${BH_MAIN_CLASS}")
  message(STATUS "  Output directory: ${LOCAL_BEETHOVEN_PATH}")
endfunction()


# Check installed location: <prefix>/share/beethoven/runtime
if(EXISTS "${_BEETHOVEN_PREFIX}/share/beethoven/runtime/src")
    set(BEETHOVEN_RUNTIME_DIR "${_BEETHOVEN_PREFIX}/share/beethoven/runtime")
    message(STATUS "Using installed Beethoven runtime: ${BEETHOVEN_RUNTIME_DIR}")
# Check source tree location: ../runtime (relative to cmake/)
elseif(EXISTS "${_BEETHOVEN_CMAKE_DIR}/../runtime/src")
    get_filename_component(BEETHOVEN_RUNTIME_DIR "${_BEETHOVEN_CMAKE_DIR}/../runtime" ABSOLUTE)
    message(STATUS "Using source tree Beethoven runtime: ${BEETHOVEN_RUNTIME_DIR}")
else()
    message(FATAL_ERROR
        "Cannot find Beethoven runtime directory.\n"
        "Checked: ${_BEETHOVEN_PREFIX}/share/beethoven/runtime\n"
        "Checked: ${_BEETHOVEN_CMAKE_DIR}/../runtime\n"
        "Please reinstall the beethoven package.")
endif()


function(beethoven_build TARGET)
  cmake_parse_arguments(BL 
    "F1"
    "PATH"
    "SOURCES;INCLUDE_DIRS"
    ${ARGN} )
  message(STATUS "Set to ${BEETHOVEN_PATH}")
  if (NOT BL_PATH)
    set(BL_PATH $ENV{BEETHOVEN_PATH})
  endif()
  if (NOT BL_SOURCES)
    message(FATAL_ERROR "No sources provided to beethoven_build.")
  endif ()
  if ("${BL_PATH}" STREQUAL "")
    message(FATAL_ERROR "Must define the BEETHOVEN_PATH environment variable!")
  endif()
  message(STATUS "PATH IS: ${BL_PATH}")
  add_executable(${TARGET} ${BL_SOURCES} ${BL_PATH}/build/beethoven_hardware.cc)
  target_include_directories(${TARGET} PUBLIC ${BL_PATH}/build/)
  target_link_libraries(${TARGET} PUBLIC APEX::beethoven)
  if (F1)
    target_link_libraries(${TARGET} fpga_mgmt)
    include_directories("$ENV{BEETHOVEN_PATH}/aws-fpga/sdk/userspace/include")
  endif()
endfunction()

function(beethoven_library TARGET)
  cmake_parse_arguments(BL "F1"
    "PATH"
    "SOURCES;INCLUDE_DIRS"
    ${ARGN} )
  if (NOT BEETHOVEN_PATH)
    set(BEETHOVEN_PATH $ENV{BEETHOVEN_PATH})
  endif()
  if (NOT BEETHOVEN_SOURCES)
    message(FATAL_ERROR "No sources provided to beethoven_build.")
  endif ()
  if ("${BEETHOVEN_PATH}" STREQUAL "")
    message(FATAL_ERROR "Must define the BEETHOVEN_PATH environment variable!")
  endif()

  add_library(${TARGET} ${BL_SOURCES} ${BL_PATH}/build/beethoven_hardware.cc)
  target_include_directories(${TARGET} PUBLIC ${BL_PATH}/build/)
  target_link_libraries(${TARGET} PUBLIC APEX::beethoven)
  if (F1)
    target_link_libraries(${TARGET} fpga_mgmt)
    include_directories("$ENV{BEETHOVEN_PATH}/aws-fpga/sdk/userspace/include")
  endif()
endfunction()

function(link_beethoven_to_target TARGET)
  cmake_parse_arguments(BEETHOVEN "F1"
    "PATH"
    ""
    ${ARGN} )
  if (NOT BEETHOVEN_PATH)
    set(BEETHOVEN_PATH $ENV{BEETHOVEN_PATH})
  endif()
  if ("${BEETHOVEN_PATH}" STREQUAL "")
    message(FATAL_ERROR "Must define the BEETHOVEN_PATH environment variable!")
  endif()

  target_sources(${TARGET} PRIVATE ${BEETHOVEN_PATH}/build/beethoven_hardware.cc)
  target_include_directories(${TARGET} PUBLIC ${BEETHOVEN_PATH}/build/)
  target_link_libraries(${TARGET} PUBLIC APEX::beethoven)
  if (F1)
    target_link_libraries(${TARGET} fpga_mgmt)
    include_directories("$ENV{BEETHOVEN_PATH}/aws-fpga/sdk/userspace/include")
  endif()
endfunction()

function(beethoven_testbench TARGET)
    cmake_parse_arguments(BT
        ""                                    # Boolean options
        "HARDWARE;SIMULATOR;DRAMSIM_CONFIG"   # Single-value args
        "SOURCES"                             # Multi-value args
        ${ARGN}
    )

    # Validate required arguments
    if(NOT BT_SOURCES)
        message(FATAL_ERROR "beethoven_testbench: SOURCES is required")
    endif()
    if(NOT BT_HARDWARE)
        message(FATAL_ERROR "beethoven_testbench: HARDWARE is required (name of beethoven_hardware target)")
    endif()

    # Check that the hardware target exists
    if(NOT DEFINED ${BT_HARDWARE}_BEETHOVEN_PATH)
        message(FATAL_ERROR
            "beethoven_testbench: '${BT_HARDWARE}' is not a valid beethoven_hardware target.\n"
            "Make sure to call beethoven_hardware(${BT_HARDWARE} ...) before beethoven_testbench().")
    endif()

    # Set defaults
    if(NOT BT_SIMULATOR)
        set(BT_SIMULATOR "verilator")
    endif()

    # Get hardware target properties
    set(HW_BEETHOVEN_PATH "${${BT_HARDWARE}_BEETHOVEN_PATH}")
    set(HW_HW_DIR "${${BT_HARDWARE}_HW_DIR}")
    set(HW_SOURCE "${${BT_HARDWARE}_SOURCE}")
    set(HW_CMAKE_SRCS "${${BT_HARDWARE}_CMAKE_SRCS}")

    message(STATUS "Configuring Beethoven testbench: ${TARGET}")
    message(STATUS "  Hardware target: ${BT_HARDWARE}")
    message(STATUS "  Simulator: ${BT_SIMULATOR}")
    message(STATUS "  Beethoven path: ${HW_BEETHOVEN_PATH}")

    add_custom_target(
      ${TARGET}_sim
      COMMAND make -f ${_BEETHOVEN_PREFIX}/share/beethoven/runtime/Makefile SIMULATOR=${BT_SIMULATOR} RUNTIME_DIR=${_BEETHOVEN_PREFIX}/share/beethoven/runtime STATIC_LIB=0 BEETHOVEN_PATH=${HW_BEETHOVEN_PATH}
      COMMAND mv BeethovenSim ${TARGET}_sim
      WORKING_DIRECTORY "${CMAKE_BUILD_DIR}"
      VERBATIM
    )

    add_dependencies(${TARGET}_sim
      ${BT_HARDWARE}_hw)

   beethoven_build(${TARGET}
      SOURCES ${BT_SOURCES}
      PATH ${HW_BEETHOVEN_PATH}
    )

    add_dependencies(${TARGET} ${TARGET}_sim)

endfunction()

